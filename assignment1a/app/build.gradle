plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
}

// For mockk with Java 16+
tasks.withType(Test) {
//    ignoreFailures = true
    jvmArgs(
//            '--add-opens", "java.base/java.lang.reflect=ALL-UNNAMED',
            '--add-opens', 'java.base/java.util.concurrent=ALL-UNNAMED',
            '--add-opens', 'java.base/java.util=ALL-UNNAMED',
            '--add-opens', 'java.base/java.net=ALL-UNNAMED')
}

android {
    namespace 'edu.vandy'

    compileSdkVersion 33

    defaultConfig {
        applicationId "edu.vandy.palantirisimulator"
        minSdk 33
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    // Ensures that any Android calls in unit tests simply
    // return a default value instead of throwing an exception.
    testOptions {
        unitTests.returnDefaultValues = true
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'

        main {
            file('src/main/res-screen')
                    .listFiles()
                    .each { res.srcDirs += it.path }
        }
    }
}

dependencies {
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.appcompat:appcompat:1.6.1'
//    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-common:2.6.1"
    implementation "androidx.preference:preference-ktx:1.2.0"
    implementation "androidx.core:core-ktx:1.10.0"
    implementation 'androidx.fragment:fragment-ktx:1.5.7'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"

    implementation 'com.google.android.material:material:1.9.0'
    implementation "com.google.code.gson:gson:2.10.1"

    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"

    implementation "com.jakewharton.rxbinding2:rxbinding:2.2.0"

    implementation 'io.apptik.widget:multislider:1.3'

    implementation "com.squareup.picasso:picasso:2.71828"

    implementation 'com.github.bumptech.glide:glide:4.15.1'
    kapt 'com.github.bumptech.glide:compiler:4.15.1'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.10"

    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'io.mockk:mockk:1.13.5'
    testImplementation "junit:junit:4.13.2"
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit:1.8.21'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.assertj:assertj-core:3.24.2'

    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
}
